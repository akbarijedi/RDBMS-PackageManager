-- =============================================
-- Author:		Mohsen Mirshahreza
-- Create date: 2023-06-01
-- Description:	Select all table or view columns
-- =============================================
CREATE OR ALTER VIEW [dbo].[Zz_UserTablesViewsColumns]
AS

SELECT 
	OBJS.NAME ParentObjectName,
	C.NAME ColumnName,
    UPPER(T.NAME) ColumnType,
    ISNULL((
		SELECT CAST(COUNT(*) AS BIT)
		FROM INFORMATION_SCHEMA.KEY_COLUMN_USAGE
		WHERE OBJECTPROPERTY(OBJECT_ID(CONSTRAINT_SCHEMA + '.' + QUOTENAME(CONSTRAINT_NAME)), 'ISPRIMARYKEY') = 1
			AND TABLE_NAME = OBJS.NAME AND COLUMN_NAME=C.NAME
	), 0) IsPrimaryKey,
    C.IS_NULLABLE AllowNull,
	(
		CASE 
			WHEN T.NAME NOT IN ('DECIMAL','NUMERIC','DATETIME2','DATETIMEOFFSET','TIME','VARCHAR','NVARCHAR','CHAR','NCHAR','VARBINARY') THEN NULL
			WHEN T.NAME IN ('NVARCHAR','NCHAR') THEN C.MAX_LENGTH/2
			ELSE C.MAX_LENGTH
		END
	) MaxLen,
	C.IS_IDENTITY IsIdentity,
	(SELECT SEED_VALUE		FROM SYS.IDENTITY_COLUMNS IDNT	WHERE IDNT.OBJECT_ID=C.OBJECT_ID AND IDNT.COLUMN_ID=C.COLUMN_ID) IdentityStart,
	(SELECT INCREMENT_VALUE FROM SYS.IDENTITY_COLUMNS IDNT	WHERE IDNT.OBJECT_ID=C.OBJECT_ID AND IDNT.COLUMN_ID=C.COLUMN_ID) IdentityStep,
	REPLACE(REPLACE(REPLACE(DEF.DEFINITION,'(',''),')',''),'''','') DbDefault,
	C.COLUMN_ID ViewOrder
FROM			SYS.COLUMNS C
INNER JOIN		SYS.TYPES T					ON C.USER_TYPE_ID = T.USER_TYPE_ID
LEFT OUTER JOIN SYS.DEFAULT_CONSTRAINTS DEF ON DEF.PARENT_OBJECT_ID = C.OBJECT_ID AND DEF.PARENT_COLUMN_ID = C.COLUMN_ID
LEFT OUTER JOIN SYS.OBJECTS OBJS ON OBJS.OBJECT_ID=C.OBJECT_ID
